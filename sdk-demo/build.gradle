apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.betomorrow.appcenter'

def getAppCenterApiToken = { ->
    project.hasProperty('appCenterApiToken') ? appCenterApiToken : ""
}

def getAppCenterOwnerName = { ->
    project.hasProperty('appCenterOwnerName') ? appCenterOwnerName : ""
}

def getAppCenterAppName = { ->
    project.hasProperty('appCenterAppName') ? appCenterAppName : ""
}

def getAppCenterDistributionGroup = { ->
    project.hasProperty('appCenterDistributionGroup') ? appCenterDistributionGroup : ""
}

android {
    defaultConfig {
        applicationId "com.paymaya.sdk.android.demo"
        compileSdk rootProject.ext.compileSdkVersion
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file('debug.keystore')
            keyAlias = 'android'
            keyPassword = 'android'
            storePassword = 'android'
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["style"]
    productFlavors {
        defaultstyle {
            applicationIdSuffix ".defaultstyle"
            versionNameSuffix "-defaultstyle"
        }
        customstyle {
            applicationIdSuffix ".customstyle"
            versionNameSuffix "-customstyle"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace 'com.paymaya.sdk.android.demo'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "com.google.android.material:material:1.11.0"

    implementation project(":sdk")
}

appcenter {
    // Api Token from AppCenter user profile
    apiToken = getAppCenterApiToken()
    // Owner Name from AppCenter Application (see following note)
    ownerName = getAppCenterOwnerName()
    // Name of the AppCenter Distribution Group
    distributionGroups = [getAppCenterDistributionGroup()]
    // Send mail to testers
    notifyTesters = true
    apps {
        defaultstyleRelease {
            appName = getAppCenterAppName()
        }
        customstyleRelease {
            appName = getAppCenterAppName()
        }
    }
}
